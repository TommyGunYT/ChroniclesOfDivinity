Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_Origins((CHARACTERGUID)S_Player_Sebille_c8d55eaf-e4eb-466a-8f0d-6a9447b5b24c);
DB_Origins((CHARACTERGUID)S_Player_Lohse_bb932b13-8ebf-4ab4-aac0-83e6924e4295);
DB_Origins((CHARACTERGUID)S_Player_Red_Prince_a26a1efb-cdc8-4cf3-a7b2-b2f9544add6f);
DB_Origins((CHARACTERGUID)S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f);
DB_Origins((CHARACTERGUID)S_Player_Fane_02a77f1f-872b-49ca-91ab-32098c443beb);
DB_Origins((CHARACTERGUID)S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978);
DB_Origins((CHARACTERGUID)S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe);
DB_Origins((CHARACTERGUID)S_Player_Kara_bcf8286e-213c-438d-b622-b6e40add12dd);

DB_UndeadRacePresets(0,"Undead_Dwarf");
DB_UndeadRacePresets(1,"Undead_Elf");
DB_UndeadRacePresets(2,"Undead_Human");
DB_UndeadRacePresets(3,"Undead_Lizard");

DB_CharacterCreationDummy((CHARACTERGUID)S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406);
DB_CharacterCreationDummy((CHARACTERGUID)S_GLO_CharacterCreationDummy_002_361dacdc-4135-4d3f-a9a2-3cad46ca246a);
DB_CharacterCreationDummy((CHARACTERGUID)S_GLO_CharacterCreationDummy_003_dded8c22-b28e-45c1-a074-eb0954602c8a);
DB_CharacterCreationDummy((CHARACTERGUID)S_GLO_CharacterCreationDummy_004_5f93cae7-6c10-4da1-b9a5-0efafc168c8e);

DB_NeutralTagIgnore("SILENTMONK");
KBSECTION
IF
GameModeStarted("Campaign",_)
THEN
ProcCheckCharacterCreationSetup();

IF
GameModeStarted("Campaign",1)
THEN
DB_InCharacterCreation(0);

IF
RegionStarted(_Lvl)
AND
IsGameLevel(_Lvl,1)
THEN
DB_InCharacterCreation(0);

IF
CharacterCreationStarted(_)
AND
CharacterAddToCharacterCreation((CHARACTERGUID)CHARACTERGUID_S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406,0,_Success)
THEN
DB_InCharacterCreation(_Success);
GoalCompleted;

IF
DB_CurrentGameMode(_Mode)
AND
_Mode != "Campaign"
THEN
GoalCompleted;

PROC
ProcCheckCharacterCreationSetup()
AND
DB_Origins(_OriginChar)
AND
IsTagged(_OriginChar,"AVATAR",1)
THEN
DB_HasAvatar(1);
DB_IsPlayer(_OriginChar);

PROC
ProcCheckCharacterCreationSetup()
AND
DB_GenericOrigins(_OriginChar)
AND
IsTagged(_OriginChar,"AVATAR",1)
THEN
DB_HasAvatar(1);
DB_IsPlayer(_OriginChar);

PROC
ProcCheckCharacterCreationSetup()
AND
DB_HasAvatar(1)
THEN
DB_InCharacterCreation(0);
GoalCompleted;

IF
DB_InCharacterCreation(1)
AND
GetRegion(CHARACTERGUID_S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406,_Region)
AND
_Region == "SYS_Character_Creation_A"
THEN
ProcSetupOriginCharacters(S_CharOriginDest_c9c5e1d7-1998-4d4e-aacb-3970e8823674);

IF
DB_InCharacterCreation(1)
AND
GetRegion(CHARACTERGUID_S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406,_Region)
AND
_Region != "SYS_Character_Creation_A"
THEN
ProcSetupOriginCharacters(CHARACTERGUID_S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406);

PROC
ProcSetupOriginCharacters((GUIDSTRING)_Dest)
AND
DB_GenericOrigins((CHARACTERGUID)_Org)
THEN
TeleportTo(_Org,_Dest);
CharacterMakePlayer(_Org, NULL_00000000-0000-0000-0000-000000000000);

PROC
ProcSetupOriginCharacters((GUIDSTRING)_Dest)
AND
DB_Origins(_Orgs)
THEN
TeleportTo(_Orgs,_Dest);

IF
DB_InCharacterCreation(1)
AND
CharacterGetHostCharacter(_Char)
AND
CharacterGetReservedUserID(_Char,_ID)
AND
GetUserProfileID(_ID,_UserName)
THEN
DB_AssignedDummyForUser(_UserName,S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406);

IF
DB_InCharacterCreation(1)
AND
DB_Origins(_Char)
THEN
CharacterMakePlayer(_Char, NULL_00000000-0000-0000-0000-000000000000);

IF
DB_InCharacterCreation(1)
THEN
ClearTag(CHARACTERGUID_S_Player_Ifan_ad9a3327-4456-42a7-9bf4-7ad60cc9e54f,"AVATAR");

IF
DB_InCharacterCreation(0)
AND
DB_CurrentLevel(_)
THEN
ProcSelectRandomStartOrigin();
ProcCheckRemoveOtherOrigins();

IF
DB_InCharacterCreation(0)
AND
DB_CharacterCreationDummy(_Npc)
AND
DB_CurrentLevel(_)
THEN
CharacterRemoveFromParty(_Npc);
CharacterMakeNPC(_Npc);
SetOnStage(_Npc,0);
NOT DB_IsPlayer(_Npc);

IF
DB_InCharacterCreation(0)
AND
DB_CurrentLevel(_)
THEN
GoalCompleted;

PROC
ProcSelectRandomStartOrigin()
AND
NOT DB_HasAvatar(1)
AND
Random(7,_Rand)
THEN
DB_SelectedStartOrigin(_Rand);

PROC
ProcSelectRandomStartOrigin()
THEN
DB_RandomOriginCounter(0);

PROC
ProcSelectRandomStartOrigin()
AND
DB_Origins(_Org)
THEN
CharacterPurgeQueue(_Org);

PROC
ProcSelectRandomStartOrigin()
AND
DB_SelectedStartOrigin(_Rand)
AND
DB_Origins(_Org)
AND
DB_RandomOriginCounter(_Ctr)
THEN
ProcTrySelectRandomStartOrigin(_Ctr,_Rand,_Org);

PROC
ProcTrySelectRandomStartOrigin((INTEGER)_Ctr,(INTEGER)_Desired,(CHARACTERGUID)_Org)
AND
_Ctr == _Desired
THEN
SetOnStage(_Org,1);
CharacterMakePlayer(_Org, NULL_00000000-0000-0000-0000-000000000000);
ProcSetupGenericOrigin(_Org);
DB_IsPlayer(_Org);
SetTag(_Org,"AVATAR");
DB_DoTeleportChosenPlayer(1);

PROC
ProcTrySelectRandomStartOrigin((INTEGER)_Ctr,(INTEGER)_Desired,(CHARACTERGUID)_Org)
AND
IntegerSum(1,_Ctr,_New)
AND
DB_RandomOriginCounter(_Old)
THEN
NOT DB_RandomOriginCounter(_Old);
DB_RandomOriginCounter(_New);

PROC
ProcSelectRandomStartOrigin()
AND
DB_RandomOriginCounter(_Ctr)
THEN
NOT DB_RandomOriginCounter(_Ctr);

PROC
ProcSetupGenericOrigin((CHARACTERGUID)S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe)
AND
Random(7,_Rand)
AND
DB_UndeadRacePresets(_Rand,_Preset)
THEN
CharacterApplyRacePreset(CHARACTERGUID_S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe,_Preset);
ObjectSetFlag(CHARACTERGUID_S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe,"DEBUG_UndeadPresetDelay");

PROC
ProcSetupGenericOrigin((CHARACTERGUID)S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe)
THEN
DB_ApplyPreset(S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe,"Conjurer");
EXITSECTION

ENDEXITSECTION
