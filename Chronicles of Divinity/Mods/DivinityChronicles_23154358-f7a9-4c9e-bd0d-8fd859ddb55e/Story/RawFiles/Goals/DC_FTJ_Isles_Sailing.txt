Version 1
SubGoalCombiner SGC_AND
INITSECTION
SetOnStage(CHARACTERGUID_DC_PlayerBoat_Beach_83985725-9cb5-479d-9284-5cc75d0520a8,0);
SetOnStage(ITEMGUID_S_DC_PlayerBoat_MainBeach_000_46af6441-3529-4f1d-bdd2-f1854ccd1ebf,1);
SetOnStage(ITEMGUID_S_DC_PlayerBoat_MainBeach_db06b8de-0f6e-45b1-9774-dbbfb205a21c,1);



//DOCKING AGENCY - (Docking Area, Docked Ship,PlayerShip Start location, PlayerShip Character,Player Dock Landing)
DB_DCBoatAgency(TRIGGERGUID_DC_FTJI_DockingArea_DragonIsle_38a3b7d2-0a68-48fe-ae4c-d0ae933a0a87, ITEMGUID_S_DC_PlayerBoat_MainBeach_000_46af6441-3529-4f1d-bdd2-f1854ccd1ebf, TRIGGERGUID_DC_FTJI_BoatStart_DragonIsle_80182983-6e1e-4d4d-8c78-47d980c5968b,TRIGGERGUID_DC_TP_FTJ_Isles_DragonIsland_7e24ada7-03ce-414e-9c3a-eb18c9c418f6);
DB_DCBoatAgency(TRIGGERGUID_DC_FTJI_DockingArea_MainIsleBeach_d8706271-7a61-4265-b356-70d79be4dfb0, ITEMGUID_S_DC_PlayerBoat_MainBeach_db06b8de-0f6e-45b1-9774-dbbfb205a21c, TRIGGERGUID_DC_FTJI_BoatStart_MainIsleBeach_7b576108-d967-4564-b3be-cf530e32f7a0,TRIGGERGUID_DC_TP_FTJ_Isles_BeachMain_fda011da-df2c-4696-868b-a9fe5591df55);

DB_DCPlayerBoat(CHARACTERGUID_DC_PlayerBoat_Beach_83985725-9cb5-479d-9284-5cc75d0520a8);

//DOCKING SHIPS - (Static boats)
DB_DockShip(ITEMGUID_S_DC_PlayerBoat_MainBeach_db06b8de-0f6e-45b1-9774-dbbfb205a21c);
DB_DockShip(ITEMGUID_S_DC_PlayerBoat_MainBeach_000_46af6441-3529-4f1d-bdd2-f1854ccd1ebf);
KBSECTION
//REGION Boat MainIsle Beach
IF
CharacterUsedItem(_Player,_DockShip)
AND
DB_IsPlayer(_Player)
AND
DB_DCPlayerBoat(_PlayerShip)
AND
DB_DCBoatAgency(_DockLoc, _DockShip, _PlayerShipStart, _LandingStart)
AND
ObjectIsInTrigger(_Player, (TRIGGERGUID)_DockLoc, 1)
THEN
TeleportTo((CHARACTERGUID)_PlayerShip,_PlayerShipStart);
SetOnStage((CHARACTERGUID)_PlayerShip,1);
CharacterMakePlayer((CHARACTERGUID)_PlayerShip);
CharacterAddToParty((CHARACTERGUID)_PlayerShip,(CHARACTERGUID)_Player);
MakePlayerActive((CHARACTERGUID)_PlayerShip);
CharacterRemoveFromParty((CHARACTERGUID)_Player);
SetCameraDistanceOverride(75.0);
GlobalSetFlag("PlayerIsBoat");
DB_ShipOwner((CHARACTERGUID)_Player);
SetOnStage((CHARACTERGUID)_Player,0);


IF
GlobalFlagSet("PlayerIsBoat")
THEN
PROC_GLO_PartyMembers_TempRemoveAll(0);

IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_DC_DockingTrigger_MainBeach_9d776048-1f28-4856-90de-d958ef2e4825)
AND
GlobalGetFlag("PlayerIsBoat",1)
THEN
CharacterAddSkill(_Player,"Shout_Docking");

IF
CharacterLeftTrigger(_Player,TRIGGERGUID_DC_DockingTrigger_MainBeach_9d776048-1f28-4856-90de-d958ef2e4825)
THEN
CharacterRemoveSkill(_Player,"Shout_Docking");

IF
CharacterUsedSkill(_Ship,"Shout_Docking","")
AND
DB_ShipOwner((CHARACTERGUID)_Player)
AND
DB_DCPlayerBoat(_PlayerShip)
AND
DB_DCBoatAgency(_DockLoc, _DockShip, _PlayerShipStart, _LandingStart)
AND
DB_IsPlayer(_Player)
AND
CharacterGetReservedUserID((CHARACTERGUID)_Player, _User)
THEN
TeleportTo((CHARACTERGUID)_Player,_LandingStart);
SetOnStage((CHARACTERGUID)_Player,1);
CharacterMakePlayer((CHARACTERGUID)_Player);
CharacterAddToParty((CHARACTERGUID)_Player,_Ship);
MakePlayerActive((CHARACTERGUID)_Player);
CharacterAssignToUser(_User, (CHARACTERGUID)_Player);
SetCameraDistanceOverride(23.0);
GlobalClearFlag("PlayerIsBoat");
CharacterRemoveFromParty(_Ship);
SetOnStage(_Ship,0);
NOT DB_ShipOwner((CHARACTERGUID)_Player);
GlobalSetFlag("DC_ShowDockedBoats");

//END_REGION

//REGION DOCKING BOAT VISIBILITY
//HIDE
IF
GlobalFlagSet("PlayerIsBoat")
THEN
SetOnStage(ITEMGUID_S_DC_PlayerBoat_MainBeach_000_46af6441-3529-4f1d-bdd2-f1854ccd1ebf,0);
SetOnStage(ITEMGUID_S_DC_PlayerBoat_MainBeach_db06b8de-0f6e-45b1-9774-dbbfb205a21c,0);
//SHOW
IF
GlobalFlagSet("DC_ShowDockedBoats")
THEN
SetOnStage(ITEMGUID_S_DC_PlayerBoat_MainBeach_000_46af6441-3529-4f1d-bdd2-f1854ccd1ebf,1);
SetOnStage(ITEMGUID_S_DC_PlayerBoat_MainBeach_db06b8de-0f6e-45b1-9774-dbbfb205a21c,1);
GlobalClearFlag("DC_ShowDockedBoats");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
