Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CharacterCreationLevels("SYS_Character_Creation_A");

DB_GLO_FirstLevelAfterCharacterCreation("TUT_Tutorial_A");

// level name, start point trigger, name of movie to play (if any)
DB_CharacterCreationTransitionInfo("FJ_FortJoy_Main",(TRIGGERGUID)TRIGGERGUID_StartPoint_001_34d67d87-441c-427d-97bb-4cc506b42fe0,"CS_Drowning");
DB_CharacterCreationTransitionInfo("TUT_Tutorial_A",(TRIGGERGUID)TRIGGERGUID_StartPoint_000__000_fe2995bf-aa16-8ce7-33a2-8cb8cf228152,"CS_Intro");

DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin2_c451954c-73bf-46ce-a1d1-caa9bbdc3cfd);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin3_41a06985-7851-4c29-8a78-398ccb313f39);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin4_41a594ed-b768-4289-9f17-59f701cc6910);
KBSECTION
IF
DB_GenericOrigins(_Orig)
THEN
SetTag(_Orig,"GENERIC");

IF
DB_InCharacterCreation(1)
THEN
IterateUsers("_CCUserSetup");

IF
UserEvent(_User,"_CCUserSetup")
AND
GetUserProfileID(_User,_UserName)
AND
DB_CharacterCreationDummy(_Dummy)
AND
NOT DB_AssignedDummyForUser(_,_Dummy)
AND
NOT DB_AssignedDummyForUser(_UserName,_)
THEN
ProcAssignDummyToUser(_Dummy,_UserName);

IF
UserEvent(_User,"_CCUserSetup")
AND
GetUserProfileID(_User,_UserName)
AND
DB_AssignedDummyForUser(_UserName,_Dummy)
THEN
CharacterAssignToUser(_User,_Dummy);
MakePlayerActive(_Dummy);

// Support "loadlevel starting_level" directly from the main menu (have same effect as
// first going through character creation)
IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_InCharacterCreation(1)
AND
DB_GLO_FirstLevelAfterCharacterCreation(_FirstLevel)
THEN
DB_CharacterCreationLevels(_FirstLevel);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_InCharacterCreation(1)
AND
DB_CurrentLevel(_Lvl)
AND
DB_CharacterCreationLevels(_Lvl)
AND
DB_GLO_FirstLevelAfterCharacterCreation(_FirstLevel)
AND
DB_CharacterCreationTransitionInfo(_FirstLevel,_StartTrigger,_Movie)
AND
DB_IsPlayer(_Char)
AND
IsTagged(_Char,"AVATAR",1)
THEN
NOT DB_DoNotFace(_Char);
ProcMovePartyToStart(_Char,_StartTrigger,_Movie);

IF
RegionStarted(_Region)
AND
IsGameLevel(_Region,1)
THEN
DB_StartedActualGame(1);

IF
UserDisconnected(_UserID,_,_UserProfile)
AND
NOT DB_InCharacterCreation(1)
AND 
NOT DB_StartedActualGame(1)
AND
DB_SelectedCC(_Char,_UserProfile)
THEN
PROC_GLO_PartyMembers_Remove(_Char,1);
ClearTag(_Char,"AVATAR");
NOT DB_IsPlayer(_Char);
ProcUnRegisterPlayerTriggers(_Char);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_InCharacterCreation(1)
THEN
//force remove this
NOT DB_InCharacterCreation(1);
ProcCheckRemoveOtherOrigins();

PROC
ProcMovePartyToStart((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
THEN
NOT DB_AlreadyTeleported(1);

PROC
ProcMovePartyToStart((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
DB_TeleportedToStartCharacters(_Char)
AND
CharacterIsInPartyWith(_Char,_NewChar,1)
THEN
DB_AlreadyTeleported(1);

PROC
ProcMovePartyToStart((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
NOT DB_AlreadyTeleported(1)
THEN
DB_TeleportedToStartCharacters(_NewChar);
PROC_GLO_CharacterCreationTeleportWithOptionalMovie(_NewChar,_StartTrigger,_Movie);

PROC
PROC_GLO_CharacterCreationTeleportWithOptionalMovie((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
_Movie != ""
THEN
CharacterTeleportPartiesToTriggerWithMovie(_StartTrigger,"",_Movie);

PROC
PROC_GLO_CharacterCreationTeleportWithOptionalMovie((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
_Movie == ""
THEN
CharacterTeleportPartiesToTrigger(_StartTrigger,"");


IF
RegionStarted("FJ_FortJoy_Main")
THEN
ProcCheckRemoveOtherOrigins();

PROC
ProcCheckRemoveOtherOrigins()
AND
NOT DB_InCharacterCreation(1)
AND
QueryOnlyOnce("SetupNonSelectedOrigins")
THEN
ProcRemoveOtherOrigins();

PROC
ProcRemoveOtherOrigins()
AND
DB_OriginRecruitmentLocation_Region("FJ_FortJoy_Main",(CHARACTERGUID)_Origin,_,_)
AND
IsTagged(_Origin,"AVATAR",0)
THEN
PROC_GLO_PartyMembers_Remove(_Origin,1);
DB_OriginNotSelected(_Origin);

PROC
ProcRemoveOtherOrigins()
AND
DB_GenericOrigins(_GenericOrigin)
AND
IsTagged(_GenericOrigin,"AVATAR",0)
THEN
CharacterRemoveFromParty(_GenericOrigin);
CharacterMakeNPC(_GenericOrigin);
SetOnStage(_GenericOrigin,0);


EXITSECTION

ENDEXITSECTION
