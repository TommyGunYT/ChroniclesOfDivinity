Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_QuestDef_State("FatherAndDaughter","Start",1);
DB_QuestDef_State("FatherAndDaughter","Poster",1);

DB_Dialogs(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888, "DC_BrandonSaved");
DB_Dialogs(CHARACTERGUID_DC_Maya_dcb9edbe-3470-428b-8684-785e5648a04d,"DC_Maya");
DB_Dialogs(CHARACTERGUID_DC_FTJ_Dima_1f927f25-9625-472e-8ba0-8a065130b4d9,"DC_Dima");

TimerLaunch("FTJ_DC_INIT", 4000);
DB_BrandonPosters(ITEMGUID_BOOK_poster_C_001_7ee87860-daf2-47a3-aaf0-fe346e396deb);
DB_BrandonPosters(ITEMGUID_BOOK_poster_C_002_c25f4c4a-4c35-4403-b58f-a0b1556daa8c);
DB_BrandonPosters(ITEMGUID_BOOK_poster_C_003_67782721-e7ab-450b-a3fb-6fa1b3de77f9);
DB_BrandonPosters(ITEMGUID_BOOK_poster_B_000_973e55e2-e2e4-4607-9d81-446ee54303dd);
DB_BrandonPosters(ITEMGUID_DC_Poster_Brandon_8fff83f0-f9e1-418b-a97f-9b5a762a2382);

SetOnStage(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888,0);
SetOnStage(ITEMGUID_DC_BrandonsBoat_Broken_f749bbe3-c284-4ce3-80d5-755905e0f339,1);
SetOnStage(ITEMGUID_DC_BrandonsBoat_Repaired_8c97e42f-653c-4a45-b51a-dbd1ebafa2d3,0);
KBSECTION
//REGION PRISONS

IF
GameBookInterfaceClosed(ITEMGUID_DC_Poster_Brandon_6637a4da-a0b9-4857-88f2-d4bca543f2e3,_Player)
AND
GlobalGetFlag("FatherAndDaughterStarted",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FatherAndDaughter_Poster");
GlobalSetFlag("FatherAndDaughterStarted");

IF
CombatStarted(_Id)
AND
CombatGetIDForCharacter(CHARACTERGUID_S_FTJ_OlgoCellarMagister_001_402470db-ad49-4de7-8a60-7f69c8e5d26e,_Id)
THEN
CharacterSetAnimationOverride(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110,"Cower_03_Loop");

IF
CombatEnded(_ID)
AND
CharacterIsDead(CHARACTERGUID_S_FTJ_OlgoCellarMagister_001_402470db-ad49-4de7-8a60-7f69c8e5d26e,1)
THEN
DisplayText(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110,"Hey! Over here! Let me out, please!");

IF
CharacterDying(CHARACTERGUID_S_FTJ_OlgoCellarMagister_001_402470db-ad49-4de7-8a60-7f69c8e5d26e)
THEN
ItemTemplateAddTo("DC_BrandonCageKey_f424e330-2c1b-4eb0-a2b5-5fcbd4faaf9f",CHARACTERGUID_S_FTJ_OlgoCellarMagister_001_402470db-ad49-4de7-8a60-7f69c8e5d26e,1);

IF
CharacterDying(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110)
THEN
GlobalSetFlag("FTJ_BrandonDied");
ItemTemplateAddTo("DC_BrandonLetterHome_a48b3447-e1b4-4976-bc0d-a487f745c8f7",CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110,1);

IF
ItemOpened(ITEMGUID_S_DC_BrandonCageDoor_4ea72f92-e6be-48da-acae-da1d9ac87847)
AND
GlobalGetFlag("FTJ_BrandonDied",0)
AND
DB_IsPlayer(_Player)
THEN
Proc_StartDialog(0,"FTJ_Brandon",CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110,_Player);

IF
DialogEnded("FTJ_Brandon",_)
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FatherAndDaughter_Start");
GlobalSetFlag("RescuedBrandon");
DisplayText(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110,"Thank you!");
TimerLaunch("BrandonFleeFromCage",3000);


IF
TimerFinished("BrandonFleeFromCage")
THEN
CharacterMoveTo(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110,TRIGGERGUID_DC_BrandonRunToPrisons_43da2dd2-674c-4a00-9e30-bc52596a773c,1,"",0);
SetOnStage(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888,1);
SetHasDialog(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110, 0);

IF
CharacterEnteredTrigger(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110, TRIGGERGUID_DC_BrandonLeavesPrison_514dbf58-9423-433b-8c2b-2f9ec170abf7)
THEN
SetOnStage(CHARACTERGUID_DC_BrandonKMiddell_773d4b3e-2094-47af-a3cd-c1101d3a1110,0);
GlobalSetFlag("DC_RescuedBrandon");
SetStoryEvent(CHARACTERGUID_DC_FTJ_Dima_1f927f25-9625-472e-8ba0-8a065130b4d9,"DimaTakeDownPoster");
//END_REGION

//REGION - BRANDON DIMA AD

IF
StoryEvent(CHARACTERGUID_DC_FTJ_Dima_1f927f25-9625-472e-8ba0-8a065130b4d9, "DimaTakeDownPoster")
THEN
SetVarFixedString(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888,"currentState","State_Default_FTJ_SeekerCamp");

//END_REGION

//REGION - MEETING BRANDON AT CAMP
IF
DialogStarted("DC_BrandonSaved",_Instance)
THEN
GlobalSetFlag("DimaBrandonVBstop");

IF
DialogStarted("DC_BrandonSaved", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
CharacterGetDisplayName(_Player, _Handle, _Ref)
THEN
DialogSetVariableStringForInstance(_Instance, "DC_GetName_abf7b032-1cc1-4341-a79c-649635affb10", _Ref);

IF
DialogEnded("DC_BrandonSaved",_)
THEN
ProcCharacterMoveTo(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888, TRIGGERGUID_DC_BrandonBeach_6f3fa649-022a-4b2a-8860-0d772156847d, 0,"DC_BrandonGoesToMaya");
PROC_RemoveDialogFromCharacter(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888);
NOT DB_Dialogs(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888,"DC_BrandonSaved");
DB_Dialogs(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888,"DC_FTJ_BrandonBeach");

IF
StoryEvent(_, "DC_BrandonGoesToMaya")
THEN
CharacterUseItem(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888, ITEMGUID_S_DC_BrandonChair_da3fe3f1-1570-4fd9-b839-760a2ea8fc78, "DC_FTJ_BrandonSatOnChair");


IF
StoryEvent(_, "DC_FTJ_BrandonSatOnChair")
THEN
NOT DB_Dialogs(CHARACTERGUID_DC_Maya_dcb9edbe-3470-428b-8684-785e5648a04d,"DC_Maya");
PROC_RemoveDialogFromCharacter(CHARACTERGUID_DC_Maya_dcb9edbe-3470-428b-8684-785e5648a04d);
DB_Dialogs(CHARACTERGUID_DC_Maya_dcb9edbe-3470-428b-8684-785e5648a04d,"DC_Maya_FatherSaved");
DisplayText(CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888, "Daddy's back, sweetheart!");
TimerLaunch("DC_FTJ_BrandonGreetsMaya", 1500);

IF
TimerFinished("DC_FTJ_BrandonGreetsMaya")
THEN
DisplayText(CHARACTERGUID_DC_Maya_dcb9edbe-3470-428b-8684-785e5648a04d, "Daddy!!");
ProcCharacterMoveTo(CHARACTERGUID_DC_Maya_dcb9edbe-3470-428b-8684-785e5648a04d, CHARACTERGUID_S_DC_GLO_Brandon_52af7d79-a56e-4a18-a7af-72d6aab2d888, 1, "DC_FTJ_MayaRunToBrandonAfterGreeting");

//END_REGION

//REGION - WANTED POSTERS
IF
CharacterUsedItem(_Player, _BrandonPosters)
AND
DB_BrandonPosters(_BrandonPosters)
AND
GlobalGetFlag("FatherAndDaughterStarted",0)
THEN
ObjectSetFlag(_Player,"QuestUpdate_FatherAndDaughter_Poster");
GlobalSetFlag("FatherAndDaughterStarted");

IF
StoryEvent(CHARACTERGUID_DC_FTJ_Dima_1f927f25-9625-472e-8ba0-8a065130b4d9,"DimaTakeDownPoster")
AND
DB_BrandonPosters(_BrandonPosters)
THEN
ItemDestroy(ITEMGUID_DC_Poster_Brandon_6637a4da-a0b9-4857-88f2-d4bca543f2e3);
ItemDestroy((ITEMGUID)_BrandonPosters);
GlobalClearFlag("FatherAndDaughterStarted");
//END_REGION

//REGION MAYA AT BEACH

IF
TimerFinished("FTJ_DC_INIT")
THEN
CharacterSetAnimationOverride(CHARACTERGUID_DC_Maya_dcb9edbe-3470-428b-8684-785e5648a04d, "Crying");
//END_REGION


IF
RegionStarted(_)
AND
DB_IsPlayer(_Player)
THEN
CameraActivate(_Player,"DC_FTJ_StartCam",0.0,0,0,0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
