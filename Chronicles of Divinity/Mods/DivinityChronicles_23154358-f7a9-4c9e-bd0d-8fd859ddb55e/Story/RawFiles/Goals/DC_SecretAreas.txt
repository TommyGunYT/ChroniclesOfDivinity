Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_QuestDef_State("TheSkeletonKey","STARTInspectedKeys",1);

CharacterUseItem(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,ITEMGUID_S_DC_SeekerCave_Chair_4a389445-2d81-8e0c-298c-c276a1708209,"");
ItemTemplateAddTo("ITEMGUID_DC_SeekerCavern_Key_a879c361-307e-423e-bef6-5243bd92f775",CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,1);

TriggerRegisterForPlayers(TRIGGERGUID_S_DC_SeekerCamp_CrimeArea_31279eca-a07d-4e93-be1b-da016799ae3d);

//ItemSetCanInteract(ITEMGUID_S_DC_SecretLushCave_HiddenDoor_c5ada3c2-6f77-41a8-b001-f225d167f0fb,0);

//REGION VISIBILITY
SetOnStage(ITEMGUID_DC_SeekerCave_Bucket_2144d42d-0bdd-4efc-a9fa-8be1b9883b01,0);
SetOnStage(ITEMGUID_DC_SecretLushCavern_Button1_21d6726b-7c40-4967-ae14-24adbe3b186d,0);
SetOnStage(ITEMGUID_DC_SecretLushCavern_Button2_68e872b2-f276-4f2d-aa7b-ed4cfc22eeda,0);
SetOnStage(ITEMGUID_DC_SecretLushCavern_Button3_82c6b337-2432-43de-8876-f5770bfecd20,0);
SetOnStage(ITEMGUID_DC_SeekerCavern_Key_000_f8e7c58e-cae3-4d3f-b636-f7a6ff3e5718,0);
//END_REGION
//REGION DBs
//Secret Lush Cave
DB_DCSecretCaveTable(TRIGGERGUID_DC_SecretCave_Table1_bc4da33f-d031-4709-9f17-18ad06c04dfa);
DB_DCSecretCaveTable(TRIGGERGUID_DC_SecretCave_Table1_bc4da33f-d031-4709-9f17-18ad06c04dfa);
DB_DCSecretCaveTable(TRIGGERGUID_DC_SecretCave_Table1_bc4da33f-d031-4709-9f17-18ad06c04dfa);

DB_DCSecretCaveButton(ITEMGUID_DC_SecretLushCavern_Button1_21d6726b-7c40-4967-ae14-24adbe3b186d);
DB_DCSecretCaveButton(ITEMGUID_DC_SecretLushCavern_Button2_68e872b2-f276-4f2d-aa7b-ed4cfc22eeda);
DB_DCSecretCaveButton(ITEMGUID_DC_SecretLushCavern_Button3_82c6b337-2432-43de-8876-f5770bfecd20);
DB_DCSecretCaveButtons(ITEMGUID_DC_SecretLushCavern_Button1_21d6726b-7c40-4967-ae14-24adbe3b186d, ITEMGUID_DC_SecretLushCavern_Button2_68e872b2-f276-4f2d-aa7b-ed4cfc22eeda, ITEMGUID_DC_SecretLushCavern_Button3_82c6b337-2432-43de-8876-f5770bfecd20);

// Seeker Cave Sneak Triggers
DB_DC_SeekerCave_SneakTrigger(TRIGGERGUID_DC_SeekerCavern_StairsApproached_d06b717b-57af-49ac-be77-5e544a312a1a);
DB_DC_SeekerCave_SneakTrigger(TRIGGERGUID_DC_SeekerCavern_StairsApproached_000_e278f2ec-5dba-48c9-a498-5fac555d22d9);
DB_DC_SeekerCave_SneakTrigger(TRIGGERGUID_DC_SeekerCavern_StairsApproached_001_7f216029-7ad0-4888-8f2c-75d4e6645757);
DB_DC_SeekerCave_SneakTrigger(TRIGGERGUID_DC_SeekerCavern_StairsApproached_002_c47301dd-e31e-4a0b-af20-3b7a2191ee2a);
//END_REGION
//REGION SEEKER CAVE
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_617a8033-a4f1-4bc7-bf00-4e1f18c900c7);
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_000_1d83ecc2-465b-497e-b1c7-8b7ca37cb061);
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_001_207bc1e3-277a-4d75-a6cb-9d1667ac3ab3);
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_002_2af2af11-2bfd-45f3-b8b5-a8f7759c7788);
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_002_f81cab92-d973-49dc-82cb-a66e5721f1c1);
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_003_021e6b85-191c-4991-8478-5f40ffac785d);
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_004_458a6008-2a95-4310-8ddd-b270f8c660da);
DB_SeekerCaveDecoKey(ITEMGUID_DC_DecoKey_SeekerCave_005_f79f5636-aa33-465d-80b4-edbad687e7e0);

//END_REGION
KBSECTION
//REGION SEEKER CAMP | WELL

IF
CharacterUsedItem(_Player,ITEMGUID_DC_WellBucket_SeekerCamp_c92f97e7-c126-43ba-8c18-4bbc40e36e79)
AND
DB_IsPlayer(_Player)
THEN
Proc_StartDialog(0,"SeekerWellBucket",ITEMGUID_DC_WellBucket_SeekerCamp_c92f97e7-c126-43ba-8c18-4bbc40e36e79,_Player);

IF
GlobalFlagSet("SeekerWellBucketPushed")
AND
GetPosition(ITEMGUID_DC_WellBucket_SeekerCamp_c92f97e7-c126-43ba-8c18-4bbc40e36e79,_X,_Y,_Z)
THEN
PlaySound(ITEMGUID_BLD_Humans_Farm_WaterWell_A_000_3ae6ac59-c29d-4814-a3b9-52992fd63cbd,"Item_Weapon_Wood_Small_Drop");
ItemDestroy(ITEMGUID_DC_WellBucket_SeekerCamp_c92f97e7-c126-43ba-8c18-4bbc40e36e79);
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01",_X,_Y,_Z);
TimerLaunch("SeekerBucketTimer",900);

IF
TimerFinished("SeekerBucketTimer")
THEN
PlaySound(ITEMGUID_BLD_Humans_Farm_WaterWell_A_000_3ae6ac59-c29d-4814-a3b9-52992fd63cbd,"Amb_Point_Dungeon_MetalHits_1");
TimerLaunch("SeekerBucketTimer2",850);

IF
TimerFinished("SeekerBucketTimer2")
THEN
PlaySound(ITEMGUID_BLD_Humans_Farm_WaterWell_A_000_3ae6ac59-c29d-4814-a3b9-52992fd63cbd,"Items_Objects_Destroy");
TimerLaunch("SeekerBucketTimer3",750);

IF
TimerFinished("SeekerBucketTimer3")
THEN
PlaySound(ITEMGUID_BLD_Humans_Farm_WaterWell_A_000_3ae6ac59-c29d-4814-a3b9-52992fd63cbd,"KillingBlow_04");
//END_REGION

//REGION SEEKER CAVE
// Bucket drops on Bjorn's head and knocks him out.
IF
GlobalFlagSet("SeekerWellBucketPushed")
THEN
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_KnockoutPoint_109133cb-669e-48a2-b55d-afc1fc87be0e,0,"DC_BjornToKnockOutPoint");

IF
StoryEvent(_,"DC_BjornToKnockOutPoint")
THEN
SetOnStage(ITEMGUID_DC_SeekerCave_Bucket_2144d42d-0bdd-4efc-a9fa-8be1b9883b01,1);
ApplyStatus(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"KNOCKED_DOWN",-1.0);

// Player enters the cavern while Bjorn is still conscious

//REGION BJORN Activity
IF
CharacterEnteredTrigger(_Player,TRIGGERGUID_DC_SeekerCavern_Entered_f8de447c-1877-49ee-addb-20301dca5f3c)
AND
GlobalGetFlag("SeekerWellBucketPushed",0)
AND
GlobalGetFlag("PlayerInSeekerCavern",0)
THEN
GlobalSetFlag("PlayerInSeekerCavern");
TimerLaunch("BjornTimer1",20000);

IF
TimerFinished("BjornTimer1")
AND
GlobalGetFlag("BjornSawPlayer",0)
AND
GlobalGetFlag("SeekerWellBucketPushed",0)
THEN
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_ChairByDiningTable_a9ee4f86-12fa-472d-9b36-51ac864e5c36,0,"DC_BjornToDiningTable");

IF
StoryEvent(_,"DC_BjornToDiningTable")
AND
GlobalGetFlag("BjornSawPlayer",0)
THEN
CharacterUseItem(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,ITEMGUID_S_DC_SeekerCavern_DiningChair_56293af0-e6a4-8eb0-174c-25fd9a125aab,"");
TimerLaunch("BjornTimer2",18500);

IF
CharacterUsedItem(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,ITEMGUID_S_DC_SeekerCavern_DiningChair_56293af0-e6a4-8eb0-174c-25fd9a125aab)
THEN
CharacterSetAnimationOverride(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"Dwarves_Hero_Male_ABC_Sit_Use_Eat_01");

IF
TimerFinished("BjornTimer2")
AND
GlobalGetFlag("SeekerWellBucketPushed",0)
THEN
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_KnockoutPoint_109133cb-669e-48a2-b55d-afc1fc87be0e,0,"DC_BjornToDesk");

IF
StoryEvent(_,"DC_BjornToDesk")
AND
GlobalGetFlag("BjornSawPlayer",0)
THEN
CharacterUseItem(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,ITEMGUID_S_DC_SeekerCave_Chair_4a389445-2d81-8e0c-298c-c276a1708209,"");
TimerLaunch("BjornTimer1",15000);
//END_REGION

IF
CharacterEnteredTrigger(_Player,_SneakTrigger)
AND
DB_IsPlayer(_Player)
AND
DB_DC_SeekerCave_SneakTrigger(_SneakTrigger)
AND
CharacterCanSee(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,_Player,1)
AND
ObjectIsInTrigger(_Player,TRIGGERGUID_DC_SeekerCavern_Check_cf857d0b-dc86-4786-9579-f0333603be7f,1)
AND
GlobalGetFlag("SeekerWellBucketPushed",0)
AND
GlobalGetFlag("BjornSawPlayer",0)
THEN
DisplayText(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"Huh... You ain't supposed to be here!");
GlobalSetFlag("BjornSawPlayer");
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_PastGates_2c99956d-13b6-4381-a3a7-ddcd8c8695fe,1,"DC_BjornToIsolation");

IF
CharacterLeftTrigger(_Player,_SneakTrigger)
AND
DB_IsPlayer(_Player)
AND
DB_DC_SeekerCave_SneakTrigger(_SneakTrigger)
AND
CharacterCanSee(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,_Player,1)
AND
ObjectIsInTrigger(_Player,TRIGGERGUID_DC_SeekerCavern_Check_cf857d0b-dc86-4786-9579-f0333603be7f,1)
AND
GlobalGetFlag("SeekerWellBucketPushed",0)
AND
GlobalGetFlag("BjornSawPlayer",0)
THEN
DisplayText(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"Huh... You ain't supposed to be here!");
GlobalSetFlag("BjornSawPlayer");
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_PastGates_2c99956d-13b6-4381-a3a7-ddcd8c8695fe,1,"DC_BjornToIsolation");

IF
StoryEvent(_,"DC_BjornToIsolation")
THEN
ProcFaceCharacter(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,ITEMGUID_DC_SeekerCavern_Door_165bb390-2fb4-85ab-48cb-d80c0c5b89e9);
ItemCloseAndLock(ITEMGUID_DC_SeekerCavern_Door_165bb390-2fb4-85ab-48cb-d80c0c5b89e9,"DC_SeekerCavern");
TimerLaunch("BjornIsClosingDoor",500);

IF
TimerFinished("BjornIsClosingDoor")
THEN
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_BehindShrine_73c4b1df-cd58-4ab5-87e6-ce6fc9363041,1,"DC_BjornReachedBackOfShrine");

IF
StoryEvent(_,"DC_BjornReachedBackOfShrine")
THEN
PlayEffect(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"RS3_FX_GP_ScriptedEvent_Ambushers_Smoke_01");
PlayEffect(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_02");
PlayEffect(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"RS3_FX_Skills_Void_Netherswap_Disappear_Root_01");
TimerLaunch("DC_BjornIsDisappearing",200);

IF
TimerFinished("DC_BjornIsDisappearing")
THEN
SetOnStage(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,0);

IF
CharacterUsedItem(_Player,ITEMGUID_DC_SeekerCavern_Inside_dd291318-a882-8023-3476-f3a30f67992b)
AND
DB_IsPlayer(_Player)
AND
GlobalGetFlag("SeekerWellBucketPushed",0)
AND
ObjectIsInTrigger(_Player,TRIGGERGUID_DC_SeekerCavern_Check_cf857d0b-dc86-4786-9579-f0333603be7f,0)
THEN
GlobalClearFlag("BjornSawPlayer");
TimerLaunch("DC_LeftSeekerCaveLuringBjorn",400);

IF
TimerFinished("DC_LeftSeekerCaveLuringBjorn")
THEN
PlayEffect(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"RS3_FX_GP_ScriptedEvent_Ambushers_Smoke_01");
PlayEffect(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,"RS3_FX_Skills_Void_Netherswap_Reappear_01");
SetOnStage(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,1);
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_PastGates_2c99956d-13b6-4381-a3a7-ddcd8c8695fe,0,"DC_BjornLeavingIsolation");

IF
StoryEvent(_,"DC_BjornLeavingIsolation")
THEN
ItemUnlockAndOpen(ITEMGUID_DC_SeekerCavern_Door_165bb390-2fb4-85ab-48cb-d80c0c5b89e9);
TimerLaunch("BjornThinksPlayerHaveLeft",200);

IF
TimerFinished("BjornThinksPlayerHaveLeft")
THEN
ProcCharacterMoveTo(CHARACTERGUID_DC_BjornTheSilent_d387bd0f-d93a-4182-a47b-ef880cd4d61a,TRIGGERGUID_DC_SeekerCavern_ChairByDiningTable_a9ee4f86-12fa-472d-9b36-51ac864e5c36,0,"DC_BjornToDiningTable");

//END_REGION

//REGION SECRET LUSH CAVE
IF
CharacterUsedItem(_Player,ITEMGUID_DC_SecretCaveLush_LeverSnake_09ad0442-9b1c-499c-8a71-9baa173c2603)
AND
DB_DCSecretCaveTable((GUIDSTRING)_TableVFX)
AND
ObjectGetFlag(ITEMGUID_DC_SecretCaveLush_TableRock_ffe21fdf-5c57-4584-bb0d-cd859f6e9fae,"RockIsOut",0)
AND
GetPosition((GUIDSTRING)_TableVFX,_TableX,_TableY,_TableZ)
THEN
ItemMoveToPosition(ITEMGUID_DC_SecretCaveLush_TableRock_ffe21fdf-5c57-4584-bb0d-cd859f6e9fae,444.83,18.62,552.24,0.5,0.5,"SecretLushRockOut");
PlayEffectAtPosition("RS3_FX_Items_Animated_Secret_WallDoor_Open_Dust_01",_TableX,_TableY,_TableZ);
ObjectSetFlag(ITEMGUID_DC_SecretCaveLush_TableRock_ffe21fdf-5c57-4584-bb0d-cd859f6e9fae,"SecretLushRockOut");
Proc_ShakeCameraForTime(_Player,3400);
TimerLaunch("SecretLushRockOut",3400);

IF
TimerFinished("SecretLushRockOut")
THEN
SetOnStage(ITEMGUID_DC_SeekerCavern_Key_000_f8e7c58e-cae3-4d3f-b636-f7a6ff3e5718,1);
ItemSetCanInteract(ITEMGUID_DC_SecretCaveLush_LeverSnake_09ad0442-9b1c-499c-8a71-9baa173c2603,0);

IF
CharacterUsedItem(_Player,ITEMGUID_S_DC_SecretLushCave_HiddenDoor_c5ada3c2-6f77-41a8-b001-f225d167f0fb)
AND
ItemIsLocked(ITEMGUID_S_DC_SecretLushCave_HiddenDoor_c5ada3c2-6f77-41a8-b001-f225d167f0fb,1)
AND
ItemIsInInventory(ITEMGUID_DC_SeekerCavern_Key_000_f8e7c58e-cae3-4d3f-b636-f7a6ff3e5718,0)
THEN
DisplayText(_Player,"Hmm. There's a keyhole there.");

IF
ItemOpened(ITEMGUID_S_DC_SecretLushCave_HiddenDoor_c5ada3c2-6f77-41a8-b001-f225d167f0fb)
THEN
UnlockSecretRegion(TRIGGERGUID_DC_SecretLushCave_SecretRegion_79dc076e-0b30-4e16-9d1d-697b5eea93d9);

//REGION BUTTON PUZZLE

IF
CharacterUsedItem(_Player, (ITEMGUID)_Button1)
AND
DB_DCSecretCaveButtons(_Button1, _Button2, _Button3)
AND
GlobalGetFlag("LushCavePuzzleSolved",0)
THEN
DisplayText(_Player,"Button 1");
//END_REGION
//END_REGION

//REGION MISC
IF
CharacterUsedItem(_Player,_SeekerCaveKeys)
AND
DB_SeekerCaveDecoKey((ITEMGUID)_SeekerCaveKeys)
THEN
DisplayText(_Player,"This dwarf seems to be crafting a lot of keys... Wonder why.");
PartySetFlag(_Player,"SeenBjornsKeys");
ObjectSetFlag(_Player,"QuestUpdate_TheSkeletonKey_STARTInspectedKeys");
//END_REGION

PROC
ProcRegionEnded("FJ_FortJoy_Main")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "FortJoy"
